---
layout: post
title:  "IBL"
date:   2022-01-05 00:50:30 +0800
tags: 图形学
color: rgb(255,90,90)
cover: '../assets/ibl/title.jpg'
subtitle: 'Image-Based Lighting'
---

# 综述

IBL 即 Image-Based Lighting，基于图像的照明，是一种实现环境光照的技术。环境光照将物体周围的整个环境视为一个大光源。在渲染方程中使用环境光源可以捕捉物体所处环境的明暗，使物体在环境中看起来更加的真实。

Split Sum Approximation 是 UE 的一种在实时渲染实现 IBL 的算法，其核心思想是将渲染方程积分拆分成分别对光照和 BRDF 的积分并预计算为 LUT ( Look Up Texture ) ，在实时渲染时直接在 LUT 中取值。

# 渲染方程

对一个微面 $p$，有：

$$
L_o(p,\omega_o)=\int_{\Omega^+}f_r(p,\omega_i,\omega_o)V(p,\omega_i)L_i(p,\omega_i)\cos\theta_id\omega_i
$$

其中：

- $\omega$ 为立体角，表示方向。$\omega_o$ 为 $p$ 与观察点的方向，$\omega_i$ 为 $p$ 与光源的方向。对立体角在半球的积分表示微面在所有方向接受入射光。
- $\theta_i$ 为光源方向和微面法线的夹角。在积分式中乘以$\cos\theta_i$将积分从半球球面投影到微面平面。
- $f_r(p,\omega_i,\omega_o)$ 为 BRDF。
- $V(p,\omega_i)$ 为微面 $p$ 对光源的可见性。一种直观的理解为：从微面 $p$ 是否可以在 $\omega_i$ 方向看到光源。
- $L$ 为辐射率（radiance）。$L_i(p,\omega_i)$ 为 $\omega_i$ 方向光源的辐射率， $L_o(p,\omega_o)$ 为微面向观测方向发出的辐射率。

在 IBL 中，默认微面对其正半球上所有方向的光源可见，即 $V(p,\omega_i)=1$。于是：

$$
L_o(p,\omega_o)=\int_{\Omega^+}f_r(p,\omega_i,\omega_o)L_i(p,\omega_i)\cos\theta_id\omega_i
$$

## 拆分

对于一个积分：

$$
\int_\Omega f(x)\cdot g(x)dx
$$

在以下任一情况中：

- $f(x)$ 的“有效积分域” **support** 很小。例如 $f(x)$ 的形状类似一个尖锐的高斯分布，仅在很窄的定义域内有值。
- $g(x)$ 很“光滑” **smooth**。这里的光滑指在积分域中函数的最大值和最小值的差小。

可以将积分近似为：

$$
\int_\Omega f(x)\cdot g(x)dx\approx\frac{\int_\Omega f(x)dx}{\int_\Omega 1dx}\cdot\int_{\Omega}g(x)dx
$$

其中分式的分母为归一化常数。例如 $f(x)$ 取常数 $f(x)=2$ ，$\Omega=[0,3]$，则：

$$
\begin{aligned}
\int_0^3 2\cdot g(x)dx&=2\int_0^3g(x)dx\\
&=\frac{\int_0^3 2dx}{\int_0^3 1dx}\int_0^3g(x)dx
\end{aligned}
$$

对于渲染方程的积分：

$$
L_o(p,\omega_o)=\int_{\Omega^+}f_r(p,\omega_i,\omega_o)L_i(p,\omega_i)\cos\theta_id\omega_i
$$

- 当微面比较粗糙时，$f_r(p,\omega_i,\omega_o)$ 是 **smooth** 的。
- $L_i(p,\omega_i)$ 的 **support** 是很小的。

于是渲染方程可以近似为：

$$
L_o(p,\omega_o)\approx\frac{\int_{\Omega^+}L_i(p,\omega_i)d\omega_i}{\int_{\Omega^+}d\omega_i}\cdot\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\cos\theta_id\omega_i
$$

如此，便可以将光照和 BRDF 拆分，分别预计算。

# 蒙特卡洛积分

对一个解析解计算困难的定积分：

$$
\int_a^b f(x)dx
$$

在定义域内对自变量随机采样：

$$
X_i\sim p(x)
$$

则该积分的蒙特卡洛积分为：

$$
F_N=\frac{1}{N}\sum_{i=1}^N\frac{f(X_i)}{p(X_i)}
$$

其中 $N$ 为采样的样本总数。

若均匀采样，有：

$$
\begin{aligned}
X_i\sim p(x)&=C\\
\int_a^b p(x)&=1\\
\end{aligned}
$$

易得：

$$
\begin{aligned}
C&=\frac{1}{b-1}\\
F_N&=\frac{b-a}{N}\sum_{i=1}^Nf(X_i)\\
\end{aligned}
$$


# 漫反射

## BRDF

漫反射采用 Lambert 模型，其 BRDF 是一个常数：

$$
\frac{\text{albedo}}{\pi}
$$

其中 $albedo$ 是一个三维向量，分别表示 R、G、B 三个通道的反照率。

BRDF 部分的积分可以直接算出：

$$
\begin{aligned}
\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\cos\theta_id\omega_i&=\frac{albedo}{\pi}\int_{\Omega^+}\cos\theta_id\omega_i\\
&=\frac{albedo}{\pi}\int_0^{2\pi}\int_0^{\frac{\pi}{2}}\cos\theta_i\sin\theta_id\theta_id\phi\\
&=\text{albedo}
\end{aligned}
$$

## 光照

###　次表面散射

次表面散射的理论为，光线在照射到物体时，有一部分被物体吸收至物体内部。被吸收的光线一部分被物体吸收（通常转化为热能），另一部分在物体内部弹射后又从物体表面散射出来。这些从物体表面散射出来的光就是漫反射的出射光线。

依照次表面散射的理论，微表面从正半球各个方向吸收光源的能量。故需要使用在半球上均匀采样的蒙特卡洛积分来求得光照部分的积分值。

### 半球上的均匀采样

随机变量的采样范围为 $\Omega^+$ ，有：

$$
\int_{\Omega^+}p(\omega)d\omega=1
$$

由均匀采样，$p(\omega)$ 为常数，有：

$$
\begin{aligned}
p(\omega)&=c\\
c\int_{\Omega^+}d\omega&=1\\
c&=\frac{1}{2\pi}\\
p(\omega)&=\frac{1}{2\pi}
\end{aligned}
$$

又：

$$
p(\theta,\phi)d\theta d\phi =p(\omega)d\omega\\
$$

由立体角定义 $dw=\sin\theta d\theta d\varphi$ 可得：

$$
\begin{aligned}
p(\theta,\phi)&=\frac{\sin\theta}{2\pi}
\end{aligned}
$$

$\theta$ 的边缘密度函数 $p(\theta)$ 为：

$$
\begin{aligned}
p(\theta)&=\int_0^{2\pi}p(\theta,\phi)d\phi\\
&=\int_0^{2\pi}\frac{\sin\theta}{2\pi}d\phi\\
&=\sin\theta
\end{aligned}
$$

$\phi$ 的条件密度函数 
$p(\phi|\theta)$
为：

$$
p(\phi|\theta)=\frac{p(\theta,\phi)}{p(\theta)}=\frac{1}{2\pi}
$$

$\theta$ 和 
$\phi|\theta$ 
的累积分布函数为：

$$
P(\hat\theta)=\int_0^\hat\theta \sin\theta d\theta=1-\cos\hat\theta
$$

$$
P(\hat\phi|\theta)=\int_0^\hat\phi \frac{1}{2\pi}d\phi=\frac{\hat\phi}{2\pi}
$$

将概率密度函数反转，取随机数 $\xi_1$ 和 $\xi_2$ 分别代表 $P(\theta)$ 和 
$P(\phi|\theta)$
 ，其中 $\xi_1,\xi_2\in[0,1]$ ，则有：

$$
\begin{aligned}
\xi_1&=1-\cos\hat\theta\\
\cos\hat\theta&=1-\xi_1
\end{aligned}
$$

由于 $\xi$ 是 $0$ 和 $1$ 之间的随机数，可用 $\xi$ 直接替换 $1-\xi$ ，则：

$$
\begin{aligned}
\cos\hat\theta&=\xi_1\\
\hat\theta&=\cos^{-1}\xi_1
\end{aligned}
$$

$$
\begin{aligned}
\xi_2&=\frac{\hat\phi}{2\pi}\\
\hat\phi&=2\pi\xi_2
\end{aligned}
$$

一种直观的理解为随机数 $\xi_1$ 是：对 $\theta$ 随机取值，$\theta$ 落在 $[0,\hat\theta]$ 内的概率。通过 $\xi_1$ 求得的 $\theta$ 的值等于 $\hat\theta$ 。$\xi_2$ 和 $\phi$ 与之相同。

将球面坐标转化为笛卡尔坐标：

$$
\begin{aligned}
x&=\sin\theta\cos\phi=\cos(2\pi\xi_2)\sqrt{1-\xi_1^2}\\
y&=\sin\theta\sin\phi=\sin(2\pi\xi_2)\sqrt{1-\xi_2^2}\\
z&=\cos\theta=\xi_1
\end{aligned}
$$

代码为：

```glsl
vec3 UniformSampleHemisphere(vec2 xi, vec3 N)
{
    vec3 sV_T;
    sV_T.x = cos(2 * PI * xi.y) * sqrt(1.0 - xi.x * xi.x);
    sV_T.y = sin(2 * PI * xi.y) * sqrt(1.0 - xi.y * xi.y);
    sV_T.z = xi.x;

    // from tangent-space sample vector to world-space sample vector
    vec3 up          = abs(N.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
    vec3 tangent   = normalize(cross(up, N));
    vec3 bitangent = cross(N, tangent);

    vec3 sampleVec = tangent * sV_T.x + bitangent * sV_T.y + N * sV_T.z;
    return normalize(sampleVec);
}
```



### 低差异序列

#### Van der Corput

一个整数 $i$ 用 $b$ 进制表示，第 $l$ 位上的数为 $a_l(i)$ ，将 $b$ 进制序列反转后移到小数点后得到 $i$ 在 Van der Corput 序列中的值。运算表达式为：

$$
\Phi_{b,I}(i)=(b^{-1}...b^{-M})(a_0(i)...a_{M-1}(i))^{\text{T}}=\sum_{l=0}^{M-1}a_l(i)b^{-l-1}
$$

当以 $2$ 为底数时，求解 Van der Corput 序列值的时候可以使用位操作：

```c++
// http://holger.dammertz.org/stuff/notes_HammersleyOnHemisphere.html
// efficient VanDerCorpus calculation.
float RadicalInverse_VdC(uint bits) 
{
     bits = (bits << 16u) | (bits >> 16u);
     bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
     bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
     bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
     bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
     return float(bits) * 2.3283064365386963e-10; // / 0x100000000
}
```

#### Hammersley

设序列点集总数为 $N$ ，Hammersley点集的定义为：

$$
X_i:=(\frac{i}{N},\Phi_{b_1}(i),...,\Phi_{b_{n-1}}(i))
$$

一个数量为 $100$ 的、以 $2$ 为底数二维 Hammersley 样本点集可以写为：

$$
(\frac{i}{100},\Phi_2(i))_{i=1}^{100}
$$


### 预计算

假设环境光照贴图保存在 environmentCubemap 中。对其在每一个方向进行半球上的平均采样。由于平均采样的方式，得到的贴图丢失了大部分高频信息，故可以使用较低的分辨率存储。

将积分写为蒙特卡洛积分：

$$
\begin{aligned}
\int_{\Omega^+}L_i(p,\omega_i)d\omega_i&=\frac{1}{N}\sum_{i=1}^N\frac{L_i(p,\omega_i)}{p(\omega_i)}\\
&=\frac{2\pi}{N}\sum_{i=1}^N L_i(p,\omega_i)\\

\end{aligned}
$$

```glsl
for(uint i = 0u; i < SAMPLE_COUNT; ++i)
{
	vec2 Xi = Hammersley(i, SAMPLE_COUNT);
    vec3 sV = UniformSampleHemisphere(Xi, N);

    irradiance += texture(environmentMap, sV).rgb;
}

irradiance /= float(SAMPLE_COUNT);
```



将结果保存在另一个 Cubemap 中，并命名为 diffuseLightLut 。

### 渲染

用微表面的法线方向在 diffuseLightLut 上取样得到拆分的渲染方程的左边部分的值。右边部分的值为常数。两者相乘后得到在环境光下完全漫反射的 $L_o$ 。

```glsl
const float PI = 3.14159265359;

in vec3 normal;
uniform samplerCube diffuseLightLut;
uniform vec3 albedo;

vec3 diffuse = texture(diffuseLightLut).rgb * albedo * (1.0 / PI);
```



# 镜面反射

## BRDF

镜面反射的 BRDF 采用微表面模型：

$$
f_r(p,\omega_i,\omega_o)=\frac{FDG}{4\cos\theta_i\cos\theta_o}
$$

其中 $F$ 为菲涅尔项，$D$ 为法向量分布函数，$G$ 为几何项。

区分微面和微表面：微面指微表面模型中一个 fragment 对应的物体表面的面积，渲染方程的积分域为微面的正半球；微表面指微表面模型概念中对一个微面进行描述的更加细分的面。

### Fresnel Term

菲涅尔项 $F$ 描述被反射的光线与光线被折射的部分的比值，此比值会随着我们观察角度的变化而变化。使用 Fresnel-Schlick Approximation 来替代复杂的菲涅尔方程：

$$
F(h,v,F_0)=F_0+(1-F_0)(1-(h\cdot v))^5
$$

其中 $F_0$ 是表面的基础反射率，表示在入射方向和观测方向皆平行与表面法线时的反射率，$v$ 是观测方向，$l$ 是光源方向，
$h=\frac{v+l}{||v+l||}$ 
。

单个 Fresnel-Schlick 近似仅仅对电介质或者说非金属表面有定义，对于导体或者说金属表面需要一个新的近似方程进行计算。由于电介质和导体通常共享一个着色模型，即材质是电介质和导体的混合体，这样的实现会产生很多不便，于是假定电介质部分的 $F_0$ 恒为 $(0.04,0.04,0.04)$ ，通过引入参数 $\text{Metalness}$ 来表示材质含有金属的比例，金属部分的 $F_0$ 为材质的颜色 $\text{albedo}$ ，插值后得到材质的基础反射率：

$$
F_0=0.04*(1-\text{Metalness})+\text{albedo}*\text{Metalness}
$$


### Normal Distribution Term

法向量分布函数从统计学上近似的表示了 $h$ 与给定半程向量方向相同的微表面和微面中全部微表面的比值。该比值与微面的粗糙度 $\text{roughness}$ 负相关。

有很多 NDF 可以估算这个比值，Trowbridge-Reitz GGX NDF 是其中之一：

$$
NDF_{\text{GGXTR}}(n,h,\alpha)=\frac{\alpha^2}{\pi((n\cdot h)^2(\alpha^2-1)+1)^2}
$$

其中 $\alpha$ 为微面的粗糙度**的平方**，$n$ 为微面法向量，$h$ 为给定的半程向量。

### Geometry Term

几何函数从统计学上近似的求得了微表面之间相互遮蔽的概率。

使用 Schlick-GGX Approximation 来计算此概率：

$$
G_{\text{Schlick-GGX}}(n,v,k)=\frac{n\cdot v}{(n\cdot v)(1-k)+k}
$$

其中 $k$ 是 $\text{roughness}$ 针对不同类型光照的重映射：

- 在直接光照下，$k=\frac{(\text{roughness}+1)^2}{8}$
- 在环境光照下，$k=\frac{\text{roughness}^2}{2}$

对光线方向和观测方向都做一次近似，再将两者相乘得到总体的遮蔽概率：

$$
G(n,v,l,k)=G_{\text{Schlick-GGX}}(n,v,k)G_{\text{Schlick-GGX}}(n,l,k)
$$

### 预计算

将 BRDF 部分的积分的值预计算并存贮在一张 2D 纹理中。

把菲涅尔项从 BRDF 中提出来：

$$
\begin{aligned}
\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\cos\theta_id\omega_i&=\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{F}{F}\cos\theta_id\omega_i\\
&=\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{F_0+(1-F_0)(1-(h\cdot \omega_o))^5}{F}\cos\theta_id\omega_i\\
\end{aligned}
$$

令 $(1-(h\cdot \omega_o))^5=t$ ，有：

$$
\begin{aligned}
\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\cos\theta_id\omega_i&=\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{F_0+(1-F_0)t}{F}\cos\theta_id\omega_i\\
&=\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{(1-t)F_0+t}{F}\cos\theta_id\omega_i\\
&=\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{(1-t)F_0}{F}\cos\theta_id\omega_i+\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{t}{F}\cos\theta_id\omega_i\\
\end{aligned}
$$

$F_0$ 为常数，可以提出积分式外：

$$
\begin{aligned}
\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\cos\theta_id\omega_i&=F_0\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{(1-t)}{F}\cos\theta_id\omega_i+\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{t}{F}\cos\theta_id\omega_i\\
&=F_0\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{(1-t)}{F}\cos\theta_id\omega_i+\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{t}{F}\cos\theta_id\omega_i\\
&=F_0\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{(1-(1-(h\cdot \omega_o))^5)}{F}\cos\theta_id\omega_i+\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\frac{(1-(h\cdot \omega_o))^5}{F}\cos\theta_id\omega_i\\
\end{aligned}
$$

将 $f_r(p,\omega_i,\omega_o)=\frac{FDG}{4\cos\theta_i\cos\theta_o}$ 带入：

$$
\int_{\Omega^+}f_r(p,\omega_i,\omega_o)\cos\theta_id\omega_i=F_0\int_{\Omega^+}\frac{DG(1-(1-(h\cdot \omega_o))^5)}{\cos\theta_o}d\omega_i+\int_{\Omega^+}\frac{DG
(1-(h\cdot \omega_o))^5}{\cos\theta_o}d\omega_i
$$

等式右侧的两个积分中：

- $D(n,h,\alpha)=\frac{\alpha^2}{\pi((n\cdot h)^2(\alpha^2-1)+1)^2}$ 。
- $G(n,\omega_o,\omega_i,k)=G_{\text{Schlick-GGX}}(n,\omega_o,k)G_{\text{Schlick-GGX}}(n,\omega_i,k)$，$k=\frac{\alpha^2}{2}$ 。
- 
$h=\frac{\omega_o+\omega_i}{||\omega_o+\omega_i||}$

- $\cos\theta_o=n\cdot\omega_o$

待积分函数的自变量为：$n$，$\omega_o$，$\omega_i$，$\alpha$ 。对 $\omega_i$ 积分，于是积分的值与 $n$，$\omega_o$，$\alpha$ 有关。

考虑切线空间，$n$ 的值恒为 $(0,0,1)$ 。

考虑 BRDF 的**各向同性**，$\omega_o$ 中仅有 $\theta_o$ 分量对结果有影响。

综上，BRDF 部分积分的值为一个二维函数，由 $\alpha$ 和 $\cos\theta_o$ 决定。

把积分写成蒙特卡洛积分，以第二个积分式为例：

$$
\begin{aligned}
\int_{\Omega^+}\frac{DG
(1-(h\cdot \omega_o))^5}{\cos\theta_o}d\omega_i&=\frac{1}{N}\sum_{k=1}^N\frac{D(n,h,\alpha)G(n,\omega_i,\omega_o,\alpha)
(1-(h\cdot \omega_o))^5}{p(\omega_i)\cos\theta_o}\\

\end{aligned}
$$

用 NDF 描述的微表面半程向量的分布对 $\omega_i$（ $l$ ）进行重要性采样。当给定观测方向时，确定了半程向量也就确定了入射光方向。

根据迪士尼的 **Physically Based Shading at Disney** 一文：

考虑半球在微面上的投影，有：

$$
\text{pdf}_h=D(\theta_h)\cos\theta_h
$$

入射光的概率密度函数和半程向量的概率密度函数之间的关系为：

$$
\text{pdf}_h=4(l\cdot h)\text{pdf}_l
$$

考虑 BRDF 的**各向同性**，$D(h)=D(\theta_h)$ 。

于是蒙特卡洛积分可以写为：

$$
\begin{aligned}
\frac{1}{N}\sum_{k=1}^N\frac{D(n,h,\alpha)G(n,\omega_i,\omega_o,\alpha)
(1-(h\cdot \omega_o))^5}{p(\omega_i)\cos\theta_o}&=\frac{1}{N}\sum_{k=1}^N\frac{D(n,h,\alpha)G(n,\omega_i,\omega_o,\alpha)
(1-(h\cdot \omega_o))^5}{\frac{p(\omega_h)}{4(\omega_i,\omega_h)}\cos\theta_o}\\
&=\frac{1}{N}\sum_{k=1}^N\frac{D(\theta_h)G(n,\omega_i,\omega_o,\alpha)
(1-(h\cdot \omega_o))^5}{\frac{D(\theta_h)\cos\theta_h}{4(\omega_i\cdot\omega_h)}\cos\theta_o}\\
&=\frac{4}{N}\sum_{k=1}^N\frac{G(1-(h\cdot \omega_o))^5(\omega_i\cdot\omega_h)}{\cos\theta_h\cos\theta_o}

\end{aligned}
$$

由于半程向量 
$h=\frac{v+l}{||v+l||}$
 ，故 $(\omega_i\cdot\omega_h)=(\omega_o\cdot\omega_h)$ 。

对 Trowbridge-Reitz GGX NDF 有：

$$
\begin{aligned}
\phi_h&=2\pi\xi_1\\
D(\theta_h)&=\frac{\alpha^2}{\pi}\frac{1}{(1+(\alpha^2-1)\cos^2\theta_h)^2}\\
\cos\theta_h&=\sqrt{\frac{1-\xi_2}{1+(\alpha^2-1)\xi_2}}
\end{aligned}
$$

其中 $\xi_1$ 和 $\xi_2$ 为在 $[0,1]$ 中的均匀采样，表示累计分布函数的值，通过 $\xi$ 求出的 $\phi$ 和 $\theta$ 是分布区间的右值：

$$
\begin{aligned}
\text{cdf}_x&=\int_0^x\text{pdf}_x dx\\
x&=\text{cdf}^{-1}(\xi)
\end{aligned}
$$

可求得特定概率下等于分布区间右值的微表面的半程向量：

$$
h=\sin\theta_h\cos\phi_h x+\sin\theta_h\sin\phi_hy+\cos\theta_hn
$$

其中 $n$ 为微面法线方向，$x$ 为切线方向，$y$ 为副切线方向。

对满足 Trowbridge-Reitz GGX NDF 的微面的微表面半程向量的重要性采样代码为：

```glsl
vec3 ImportanceSampleGGX(vec2 Xi, vec3 N, float roughness)
{
    float alpha = roughness * roughness;
    
    float phi = 2.0 * PI * Xi.x;
    float cosTheta = sqrt((1.0 - Xi.y) / (1.0 + (alpha * alpha - 1.0) * Xi.y));
    float sinTheta = sqrt(1.0 - cosTheta * cosTheta);
    
    // from spherical coordinates to cartesian coordinates - halfway vector
    vec3 H;
    H.x = cos(phi) * sinTheta;
    H.y = sin(phi) * sinTheta;
    H.z = cosTheta;
    
    // from tangent-space H vector to world-space sample vector
    vec3 up          = abs(N.z) < 0.999 ? vec3(0.0, 0.0, 1.0) : vec3(1.0, 0.0, 0.0);
    vec3 tangent   = normalize(cross(up, N));
    vec3 bitangent = cross(N, tangent);
    
    vec3 sampleVec = tangent * H.x + bitangent * H.y + N * H.z;
    return normalize(sampleVec);
}
```

继而求出根据 NDF 描述的半程向量分布进行重要性采样的入射光方向：

$$
l=2(v\cdot h)h-v
$$

其中 $l=\omega_i$ ，$v=\omega_o$ 。

至此已得到计算蒙特卡洛积分所需的所有变量的值。

计算 BRDF 部分积分预计算的代码为：

```glsl
vec2 IntegrateBRDF(float NdotV, float roughness)
{
    // 根据cosTheta求出V
    // 由于各向同性不考虑Phi
    vec3 V;
    V.x = sqrt(1.0 - NdotV * NdotV);
    V.y = 0.0;
    V.z = NdotV;

    // A为F0提出积分式后的第一项积分
    // B为F0提出积分式后的第二项积分
    float A = 0.0;
    float B = 0.0; 

    // 对一个N为(0,0,1)的微面进行计算
    vec3 N = vec3(0.0, 0.0, 1.0);
    
    const uint SAMPLE_COUNT = 1024u;
    for(uint i = 0u; i < SAMPLE_COUNT; ++i)
    {
        // 对Phi和Theta的概率均匀采样
        vec2 Xi = Hammersley(i, SAMPLE_COUNT);
        // 得到Trowbridge-Reitz GGX NDF分布的微表面法向量
        vec3 H = ImportanceSampleGGX(Xi, N, roughness);
        // 转化为入射光方向
        vec3 L = normalize(2.0 * dot(V, H) * H - V);

        // 求出计算蒙特卡洛积分所需的量
        float NdotL = max(L.z, 0.0);
        float NdotH = max(H.z, 0.0);
        float VdotH = max(dot(V, H), 0.0);

        if(NdotL > 0.0)
        {
            float G = geometry(N, V, L, roughness);
            // VdotH == LdotH
            float G_Vis = (G * VdotH) / (NdotH * NdotV);
            float Fc = pow(1.0 - VdotH, 5.0);

            A += (1.0 - Fc) * G_Vis;
            B += Fc * G_Vis;
        }
    }

    A /= float(SAMPLE_COUNT);
    B /= float(SAMPLE_COUNT);
    
    return vec2(A, B);
}
```

计算结果为：

![brdfLut]({{site.url}}\assets\ibl\brdfLut-LdotH.jpg)

## 光照

### 预计算

光照的预计算同样要对入射光以 NDF 描述的分布进行重要性采样。

假设光线的反射方向 $r$ 与微面法线方向 $n$ 相同。当微面完全光滑时，易得入射光方向 $l$ 与二者平行；当微面非常粗糙时，微面正半球上各个方向的入射光都可能被反射至法线方向。

对 NDF 进行采样。NDF 自身就是微表面半程向量的分布函数，考虑半球到微面的投影，有：

$$
\text{pdf}_h=D(\theta_h)\cos\theta_h
$$

对 Trowbridge-Reitz GGX NDF 有：

$$
\begin{aligned}
\phi_h&=2\pi\xi_1\\
D(\theta_h)&=\frac{\alpha^2}{\pi}\frac{1}{(1+(\alpha^2-1)\cos^2\theta_h)^2}\\
\cos\theta_h&=\sqrt{\frac{1-\xi_2}{1+(\alpha^2-1)\xi_2}}
\end{aligned}
$$

其中 $\xi_1$ 和 $\xi_2$ 为在 $[0,1]$ 中的均匀采样，表示累计分布函数的值，即微表面半程向量落在某个区间内的概率。区间的换算成球面坐标后，区间左值为 $0$ ，右值为半程向量和法向量的夹角 $\theta$ （ $\phi$ 均匀分布）。区间的右值也是半程向量的采样。通过半程向量和 $r$ 得到入射光的采样。

将积分写成蒙特卡洛积分：

$$
\begin{aligned}
\int_{\Omega^+}L_i(p,\omega_i)d\omega_i&=\frac{1}{N}\sum_{k=1}^N\frac{L_i(p,\omega_i)}{p(\omega_i)}\\
&=\frac{4}{N}\sum_{k=1}^N\frac{Li(p,\omega_i)(\omega_i\cdot\omega_h)}{D(\theta_h)\cos\theta_h}
\end{aligned}
$$

对满足 Trowbridge-Reitz GGX NDF 的微面的微表面半程向量的重要性采样代码与 BRDF 部分相同。

计算光照部分积分预计算的代码为：

```glsl
uniform vec3 worldPos;
uniform float roughness;

vec3 N = normalize(WorldPos);
vec3 R = N;

vec3 prefilteredColor = vec3(0.0);
const uint SAMPLE_COUNT = 1024u;
for(uint i = 0u; i < SAMPLE_COUNT; ++i)
{
    vec2 Xi = Hammersley(i, SAMPLE_COUNT);
    vec3 H = ImportanceSampleGGX(Xi, N, roughness);
    vec3 L  = normalize(2.0 * dot(R, H) * H - R);
    
    float NdotL = max(dot(N, L), 0.0);
    if(NdotL > 0.0)
    {
        float D = ggxtr(N, H, roughness);
        float NdotH = max(dot(N, H), 0.0);
        float LdotH = max(dot(L, H), 0.0);
        float pdf = D * NdotH;
        
        prefilteredColor += texture(environmentMap, L).rgb * LdotH / pdf;
    }
}

prefilteredColor *= 4;
prefilteredColor /= float(SAMPLE_COUNT);
prefilteredColor /= 2 * PI;
```

### 渲染

在预计算时，不直接采样环境贴图，而是采样环境贴图的 mipmap 。这样可以减少采样次数，并有效地解决在较粗糙的 mip 级别上由于采样不够而导致明亮区域周围出现点状图案的问题。

在生成环境贴图时开启三线性插值，然后生成 mipmap ：

```c++
glBindTexture(GL_TEXTURE_CUBE_MAP, envCubemap);
glTexParameteri(GL_TEXTURE_CUBE_MAP, GL_TEXTURE_MIN_FILTER, GL_LINEAR_MIPMAP_LINEAR); 
...
glGenerateMipmap(GL_TEXTURE_CUBE_MAP);
```

采样的 mipmap level 由单个样本表示的立体角 **saSample** 与单个纹素在最高分辨率贴图覆盖的立体角 **saTexel** 之间的比值确定：

```glsl
vec3 prefilteredColor = vec3(0.0);
float totalWeight = 0.0;

float NdotL = max(dot(N, L), 0.0);
if(NdotL > 0.0)
{
    // ...
    
    float pdf = D * NdotH / (4.0 * HdotV) + 0.0001;

	float saSample = 1.0 / (float(SAMPLE_COUNT) * pdf + 0.0001);
	float saTexel  = 4.0 * PI / (6.0 * resolution * resolution);
	float mipBias = 1.0f;
	float mipLevel = max(0.5 * log2(saSample / saTexel) + mipBias, 0.0f);

	prefilteredColor += textureLod(environmentMap, L, mipLevel).rgb * NdotL;
    totalWeight      += NdotL;
}

prefilteredColor = prefilteredColor / totalWeight;
```



# 渲染

最终的渲染方程为：

$$
L_o=\int_{\Omega^+}(k_d\frac{\text{albedo}}{\pi}+k_s\frac{DFG}{4\cos\theta_i\cos\theta_o})L_i\cos\theta_id\omega_i
$$

其中 $k_d$ 表示材质进行漫反射的比例，$k_s$ 表示材质进行镜面反射的比例。

由于材质中的电介质部分进行漫反射，有：

$$
k_d=(1-\text{metalness})
$$

由于镜面 BRDF 中的 Fresnel Term 表示了物体表面的反射率，故：

$$
k_d=(1-F)(1-\text{metalness})
$$

且 $k_s$ 直接由 Fresnel Term 表示。

$$
L_o=\int_{\Omega^+}(1-F)(1-\text{metalness})\frac{\text{albedo}}{\pi}L_i\cos\theta_id\omega_i  +\int_{\Omega^+}\frac{DFG}{4\cos\theta_i\cos\theta_o}L_i\cos\theta_id\omega_i
$$

两个积分式拆分成光照积分和 BRDF 积分的乘积，而拆分后的积分都已经预计算，渲染时仅查表取值再做乘积即可：

```glsl
vec3 f0 = mix(vec3(0.04), albedo, metallic);
vec3 FresnelTerm = fresnel_schlick_ibl(f0, view, normal);
vec3 diffIrradiance = texture(diffuseIrradiance, normal).rgb;
diffuse = (1. - FresnelTerm) * (1. - metallic) * albedo * (1 / PI) * diffIrradiance;
vec3 R = reflect(-view, normal);
const float MAX_REFLECTION_LOD = 4.0;
vec3 specIrradiance = textureLod(prefilterMap, R, roughness * MAX_REFLECTION_LOD).rgb;
vec2 brdf = texture(brdfLut, vec2(max(dot(normal, view), 0.0), roughness)).rg;
specular = specIrradiance * (FresnelTerm * brdf.x + brdf.y);

Lo += diffuse + specular;
```

最终的渲染结果由题图所示。